group 'com.tkachuko'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.4-3'
    ext.kotlinx_html_version = '0.6.4'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

apply plugin: 'kotlin2js'
apply plugin: 'com.moowork.node'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-html-js:$kotlinx_html_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

task wrapper(type: Wrapper) {
    gradleVersion = "4.1"
}

[compileKotlin2Js, compileTestKotlin2Js]*.configure {
    kotlinOptions.moduleKind = "commonjs"
}

task populateNodeModules(type: Copy, dependsOn: compileKotlin2Js) {
    from compileKotlin2Js.destinationDir

    configurations.testCompile.each {
        from zipTree(it.absolutePath).matching { include '*.js' }
    }

    into "${buildDir}/node_modules"
}

node {
    download = true
}

task installMocha(type: NpmTask) {
    args = ['install', 'mocha']
}

task runMocha(type: NodeTask, dependsOn: [compileTestKotlin2Js, populateNodeModules, installMocha]) {
    script = file('node_modules/.bin/mocha')
    args = [compileTestKotlin2Js.outputFile]
}

test.dependsOn runMocha

task installBrowserify(type: NpmTask) {
    args = ['install', 'browserify']
}

task runBrowserify(type: NodeTask, dependsOn: [compileKotlin2Js, populateNodeModules, installBrowserify]) {
    script = file('node_modules/.bin/browserify')
    args = ["${buildDir}/node_modules/${project.name}_main.js", "-o", "bundle.js"]
}

build.dependsOn runBrowserify